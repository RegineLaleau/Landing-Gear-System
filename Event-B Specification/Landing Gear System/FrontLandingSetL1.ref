REFINEMENT
	FrontLandingSetL1
REFINES
	FrontLandingSet_Interface

SEES
	FrontLandingSet_CONT,
	MechanicalSubSystem_CONT,
	LandingGearSystemL1_CONT,
	LandingGearSystemL0_CONT
VARIABLES
	doorState,
	dubState,
	gearState,
	hubState,
	lbState,
	flsState
INVARIANT
	doorState : Door --> DoorSTATES &
	dubState : DownUplockBox --> DownUplockBoxSTATES &
	gearState : Gear --> GearSTATES &
	hubState : HighUplockBox --> HighUplockBoxSTATES &
	lbState : LatchingBoxes --> LatchingBoxesSTATES
INITIALISATION
	doorState :: {door} -->DoorSTATES ||
	dubState :: {dub} -->DownUplockBoxSTATES ||
	gearState :: {gear} -->GearSTATES ||
	hubState :: {hub} -->HighUplockBoxSTATES ||
	lbState :: {lb} -->LatchingBoxesSTATES ||
	flsState :: {fls} -->FrontLandingSetSTATES

EVENTS
	CloseDownUplockBox =
	SELECT
		gearState(gear)=gextended &
		dubState(dub)=openedDBox
	THEN
		dubState(dub):=closedDBox
	END
 ;
	CloseFrontDoor =
	SELECT
		dubState(dub)=closedDBox &
		doorState(door)=dopen
	THEN
		doorState(door):=dclose
	END
 ;
	OpenFrontDoor =
	SELECT
		lbState(lb)=lbopened &
		doorState(door)=dclose
	THEN
		doorState(door):=dopen
	END
 ;
	CloseLatchingBox =
	SELECT
		doorState(door)=dclose &
		lbState(lb)=lbopened
	THEN
		lbState(lb):=lbclosed
	END
 ;
	OpenLatchingBox =
	SELECT
		lbState(lb)=lbclosed
	THEN
		lbState(lb):=lbopened
	END
 ;
	OpenHighUplockBox =
	SELECT
		doorState(door)=dopen &
		hubState(hub)=closedHBox
	THEN
		hubState(hub):=openedHBox
	END
 ;
	ExtendsFrontGear ref ExtendsFrontLandingSet=
	SELECT
		hubState(hub)=openedHBox &
		gearState(gear)=gretracted &
		flsState(fls)=retractedFLS
	THEN
		gearState(gear):=gextended ||
		flsState(fls):=extendedFLS
	END
END

